apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.forrestguice.suntimescalendars"
        //noinspection MinSdkTooLow
        minSdkVersion 14
        //noinspection ExpiredTargetSdkVersion,OldTargetApi
        targetSdkVersion 28
        versionCode 19
        versionName "0.6.1"

        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
        resConfigs "ca", "cs", "de", "en", "eo", "es", "eu", "fr", "hu", "it", "nb", "nl", "pl", "pt", "ru", "zh"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions
    {
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-compat:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation project(path: ":lib-quadflask-colorpicker")  //implementation 'com.github.forrestguice:colorpicker:0.0.13post2'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.jraska:falcon:2.2.0'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy {
        force 'com.android.support:support-annotations:25.4.0'
    }
}

def getGitHash()
{
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
